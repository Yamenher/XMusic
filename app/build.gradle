plugins {
	id 'com.android.application'
}

android {
	compileSdk 36
	
	useLibrary 'org.apache.http.legacy'
	
	defaultConfig {
		applicationId "com.xapps.media.xmusic"
		namespace "com.xapps.media.xmusic"
		minSdkVersion 26
		targetSdkVersion 36
		versionCode 200
		versionName "0.2 Alpha"
        
        android.buildFeatures.buildConfig true

		def localProps = new Properties()
		def localPropsFile = rootProject.file("local.properties")
		if (localPropsFile.exists()) {
			localProps.load(new FileInputStream(localPropsFile))
		}

		def getSecret = { key ->
			if (localProps[key] != null) return localProps[key]
			else if (System.getenv(key) != null) return System.getenv(key)
			else return ""
		}

		buildConfigField "String", "TG_BOT_TOKEN", "\"${getSecret('TG_BOT_TOKEN')}\""
		buildConfigField "String", "TG_CHAT_ID", "\"${getSecret('TG_CHAT_ID')}\""
	}
	
	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
		}
	}
	
	buildFeatures {
		 viewBinding true
	}
	
	compileOptions {
         sourceCompatibility JavaVersion.VERSION_17
         targetCompatibility JavaVersion.VERSION_17
    }

    splits {
        abi {
            enable true
            reset()
            include "armeabi-v7a", "arm64-v8a"
            universalApk false
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def abi = output.getFilter(com.android.build.OutputFile.ABI)
            if (abi != null) {
                output.outputFileName = "${variant.name}-${abi}.apk"
            }
        }
    }
}

dependencies {
        implementation fileTree(dir: 'libs', include: ['*.jar'])
    	implementation 'com.github.bumptech.glide:glide:5.0.3'
    	implementation 'com.google.code.gson:gson:2.13.1'
        implementation 'com.google.android.material:material:1.13.0'
    	implementation 'com.github.mmmelik:RoundedImageView:v1.0.1'
        implementation 'androidx.transition:transition:1.6.0'
        implementation 'androidx.fragment:fragment:1.8.9'
    	implementation 'androidx.palette:palette:1.0.0'
    	implementation 'androidx.media3:media3-common:1.8.0'
        implementation 'androidx.media3:media3-exoplayer:1.8.0'
        implementation 'androidx.media3:media3-session:1.8.0'
        implementation 'androidx.core:core-splashscreen:1.2.0-rc01'
        implementation 'androidx.activity:activity:1.11.0'
        implementation 'androidx.media:media:1.7.1'
        implementation 'androidx.customview:customview-poolingcontainer:1.0.0'
        implementation 'androidx.recyclerview:recyclerview:1.4.0'
        implementation 'com.github.bumptech.glide:recyclerview-integration:4.14.2'
        implementation 'androidx.navigation:navigation-ui-ktx:2.9.5'
        implementation 'androidx.navigation:navigation-fragment:2.9.5'
        implementation 'org.jellyfin.media3:media3-ffmpeg-decoder:1.8.0+1'
        implementation 'me.zhanghai.android.fastscroll:library:1.3.0'
        implementation project(':vumeterlibrary')
		implementation project(':fastui')
}