plugins {
    id 'com.android.application'
}

android {
    namespace "com.xapps.media.xmusic"
    compileSdk 36

    defaultConfig {
        applicationId "com.xapps.media.xmusic"
        minSdk 26
        targetSdk 36
        versionCode 24
        versionName "0.24 Alpha"

        def localProps = new Properties()
        def localPropsFile = rootProject.file("local.properties")
        if (localPropsFile.exists()) {
            localProps.load(new FileInputStream(localPropsFile))
        }

        def getSecret = { key ->
            if (localProps[key] != null) return localProps[key]
            else if (System.getenv(key) != null) return System.getenv(key)
            else throw new GradleException("$key is missing.")
        }

        buildConfigField "String", "TG_BOT_TOKEN", "\"${getSecret('TG_BOT_TOKEN')}\""
        buildConfigField "String", "TG_CHAT_ID", "\"${getSecret('TG_CHAT_ID')}\""
        buildConfigField "String", "LOG_CHAT_ID", "\"${getSecret('LOG_CHAT_ID')}\""
    }

    buildFeatures {
        viewBinding true
       // compose true
        buildConfig true
    }
    


    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    signingConfigs {
        def localProps = new Properties()
        def localPropsFile = rootProject.file("local.properties")
        if (localPropsFile.exists()) {
            localProps.load(new FileInputStream(localPropsFile))
        }

        def getSecret = { key ->
            if (localProps[key] != null) return localProps[key]
            else if (System.getenv(key) != null) return System.getenv(key)
            else throw new GradleException("$key is missing.")
        }

        release {
            def base64Key = getSecret("KEYSTORE_BASE64")
            def tmpFile = file("$buildDir/tmp_keystore.jks")
            tmpFile.parentFile.mkdirs()
            tmpFile.bytes = Base64.decoder.decode(base64Key)
            storeFile tmpFile
            storePassword getSecret("KEYSTORE_PASSWORD")
            keyAlias getSecret("KEY_ALIAS")
            keyPassword getSecret("ALIAS_PASSWORD")
        }

        debug {
            initWith release
        }

        preview {
            initWith release
        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
        }

        preview {
            debuggable false
            minifyEnabled false
            shrinkResources false
            signingConfig signingConfigs.preview
        }

        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            //proguardFiles
                //    getDefaultProguardFile("proguard-android-optimize.txt"),
                  //  "proguard-rules.pro"
        }
    }

    lint {
        abortOnError false
        checkReleaseBuilds false
    }

    splits {
        abi {
            enable true
            reset()
            include "armeabi-v7a", "arm64-v8a"
            universalApk true
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def abi = output.getFilter(com.android.build.OutputFile.ABI)
            if (abi != null) {
                output.outputFileName = "${variant.name}-${abi}.apk"
            }
        }
    }
}

repositories {
    flatDir {
        dirs "libs"
    }
}

dependencies {

    // =======================
    // Compose
    // =======================
   // implementation platform("androidx.compose:compose-bom:2026.01.01")
  //  implementation "androidx.compose.ui:ui"
  //  implementation "androidx.compose.material3:material3"
   // implementation "androidx.activity:activity-compose"
  //  implementation 'com.tencent:mmkv-static:1.3.5'
  //  implementation "com.github.FunnySaltyFish.ComposeDataSaver:data-saver:1.1.9"

    implementation "com.github.bumptech.glide:glide:5.0.3"
    implementation "com.google.code.gson:gson:2.13.1"
    implementation "com.google.android.material:material:1.14.0-alpha06"
    implementation "androidx.transition:transition:1.6.0"
    implementation "androidx.fragment:fragment:1.8.9"
    implementation "androidx.palette:palette:1.0.0"

    implementation "androidx.media3:media3-common:1.8.0"
    implementation "androidx.media3:media3-exoplayer:1.8.0"
    implementation "androidx.media3:media3-session:1.8.0"

    implementation "androidx.core:core-splashscreen:1.2.0"
    implementation "androidx.activity:activity:1.11.0"
    implementation "androidx.media:media:1.7.1"
    implementation "androidx.customview:customview-poolingcontainer:1.0.0"
    implementation "androidx.recyclerview:recyclerview:1.4.0"
    implementation "androidx.lifecycle:lifecycle-process:2.10.0"

    implementation "org.jellyfin.media3:media3-ffmpeg-decoder:1.8.0+1"
    implementation "com.github.rtugeek:colorseekbar:2.0.3"
    implementation "androidx.graphics:graphics-shapes:1.1.0"
    implementation "androidx.preference:preference:1.2.1"
    implementation "io.github.chayanforyou:marquee:1.0.0"
    implementation "com.github.Adonai:jaudiotagger:2.3.14"
    implementation "com.airbnb.android:lottie:6.7.1"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.2.0"

    implementation(name: "fastscroller", ext: "aar")
    implementation(name: "roundedimageview", ext: "aar")
}